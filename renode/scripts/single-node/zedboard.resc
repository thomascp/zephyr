:name: Zedboard
:description: This script runs Linux on Zedboard.

using sysbus
$name?="Zedboard"
mach create $name

machine LoadPlatformDescription @platforms/boards/zedboard.repl

sysbus Redirect 0xC0000000 0x0 0x10000000

$bin?=@http://antmicro.com/projects/renode/zedboard-console--vmlinux-zedboard--f85b95c95b7c8c2e34db859d255d9632fd15a8c9
$rootfs?=@http://antmicro.com/projects/renode/zedboard-console--ramdisk8M.image-s_3699347-af6ba178f389fff0ecba89170124abf99f58f904
$dtb?=@http://antmicro.com/projects/renode/zedboard-console--devicetree-zedboard.dtb--7f202b8521d5c0e1b2120e07e677591b8917c788
### create externals ###

showAnalyzer sysbus.uart1

emulation CreateSwitch "switch"
connector Connect gem0 switch

## set timer frequency ##

ttc0 SetFrequency 0 33333333
ttc0 SetFrequency 1 33333333
ttc0 SetFrequency 2 33333333
ttc1 SetFrequency 0 33333333
ttc1 SetFrequency 1 33333333
ttc1 SetFrequency 2 33333333
macro reset
"""
    ### set registers ###

    cpu SetRegisterUnsafe 0 0x000
    cpu SetRegisterUnsafe 1 0xD32 # board id
    cpu SetRegisterUnsafe 2 0x100 # device tree address

    ### load binaries ###

    sysbus LoadELF $bin
    sysbus LoadBinary $dtb 0x100
    sysbus ZeroRange 0x1f645000 0x800000
    sysbus LoadBinary $rootfs 0x1f645000
"""

runMacro $reset
